import java.text.SimpleDateFormat

plugins {
    id "org.ajoberstar.grgit" version "5.0.0"
}

jar {
    archiveBaseName = 'metadata-logger'
    archiveVersion = '0.0.1'
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Build-Timestamp': new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date()),
                'Created-With': "Gradle ${gradle.gradleVersion}",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}",
                'Build-Revision': "${grgit.head().id}",
                'Build-Branch': "${grgit.branch.current().fullName}"
        )
    }
}

jar.dependsOn('buildMetadataFile')
tasks.register('buildMetadataFile') {

    logger.info("Creating metadata file for inclusion in final artefact")

    ext {
        outDir = "$buildDir/resources/main"
    }
    doLast {
        mkdir outDir
        new File(outDir, "build.info").text = """
BuildTime: ${new SimpleDateFormat("dd-MM-yyyy HH:mm:ss").format(new Date())}
ArchiveBaseName: ${jar.archiveBaseName.get()}
ArchiveVersion: ${jar.archiveVersion.get()}
VersionForFile: ${VersionForFile}
GitCommitId: ${grgit.head().id}
GitBranch: ${grgit.branch.current().fullName}
"""
    }
}